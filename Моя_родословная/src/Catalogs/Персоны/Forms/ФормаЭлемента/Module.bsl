
&НаКлиенте
Процедура АдресФотографииНажатие(Элемент, СтандартнаяОбработка)
	
	Если ПустаяСтрока(АдресФотографии) Тогда //добавляем фотографию
	
		НачатьВыборФотографии();		
	
	Иначе //фотография уже есть, предлагаем либо заменить фотографи, либо удалить её
	
		СписокВыбора = Новый СписокЗначений;
		СписокВыбора.Добавить("Заменить фотографию");
		СписокВыбора.Добавить("Удалить фотографию");
		СписокВыбора.Добавить("Отмена");
		
		ООЗавершениеВыбораВариантаРаботыСФотографией = Новый ОписаниеОповещения("ЗавершениеВыбораВариантаРаботыСФотографией", ЭтаФорма);
		ПоказатьВыборИзМеню(ООЗавершениеВыбораВариантаРаботыСФотографией, СписокВыбора);
		
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьВыборФотографии()
	
	ООЗавершениеПомещенияФайла = Новый ОписаниеОповещения("ЗавершениеПомещенияФайла", ЭтаФорма);
	НачатьПомещениеФайла(ООЗавершениеПомещенияФайла, , , Истина, ЭтаФорма.УникальныйИдентификатор);
	
КонецПроцедуры	

&НаКлиенте
Процедура ЗавершениеПомещенияФайла(Результат, АдресПомещенногоФайла, ВыбранноеИмяФайла, ДополнительныйПараметр) Экспорт
	
	Если Результат = Истина Тогда
		
		АдресФотографии = АдресПомещенногоФайла;
		Модифицированность = Истина;
		
	КонецЕсли;	
		
КонецПроцедуры	

&НаКлиенте
Процедура ЗавершениеВыбораВариантаРаботыСФотографией(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЭлемент <> Неопределено Тогда
	
		Если ВыбранныйЭлемент.Значение = "Заменить фотографию" Тогда
			
			НачатьВыборФотографии();
			
		ИначеЕсли (ВыбранныйЭлемент.Значение = "Удалить фотографию") Тогда
			 
			АдресФотографии = "";
			Модифицированность = Истина;
			
		КонецЕсли;	
		
	КонецЕсли;	
	
КонецПроцедуры	

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Не ПустаяСтрока(АдресФотографии) Тогда
		
		Попытка
			ТекущийОбъект.ХранилищеФотографии = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(АдресФотографии));
		Исключение
		КонецПопытки;
		
	Иначе
			
		ТекущийОбъект.ХранилищеФотографии = Неопределено;	
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Если ТекущийОбъект.ХранилищеФотографии <> Неопределено Тогда
		
		ДвоичныеДанныеФотографии = ТекущийОбъект.ХранилищеФотографии.Получить();
		
		Если ДвоичныеДанныеФотографии <> Неопределено Тогда
			АдресФотографии = ПоместитьВоВременноеХранилище(ТекущийОбъект.ХранилищеФотографии.Получить());
		КонецЕсли;
		
	КонецЕсли;
	
	УстановитьФорматРедактированияДаты(Элементы.ДатаРождения, ТекущийОбъект.ТочностьДатыРождения);
	УстановитьФорматРедактированияДаты(Элементы.ДатаСмерти, ТекущийОбъект.ТочностьДатыСмерти);
	УстановитьВидимостьГруппыДатыСмерти(Элементы.ГруппаДатыСмерти, Не ТекущийОбъект.НынеЖив);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьФорматРедактированияДаты(Элемент, ТочностьДаты)
	                                                          
	Если ТочностьДаты = ПредопределенноеЗначение("Перечисление.ТочностьДаты.Год") Или ТочностьДаты = ПредопределенноеЗначение("Перечисление.ТочностьДаты.Десятилетие") Тогда
		Элемент.ФорматРедактирования = "ДФ='yyyy'";
	ИначеЕсли ТочностьДаты = ПредопределенноеЗначение("Перечисление.ТочностьДаты.Месяц") Тогда	
		Элемент.ФорматРедактирования = "ДФ='MM yyyy'";
	Иначе
		Элемент.ФорматРедактирования = "";
	КонецЕсли;	
	
КонецПроцедуры	

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьГруппыДатыСмерти(Элемент, Видимость)
	                                                          
	Элемент.Видимость = Видимость;	
	
КонецПроцедуры

&НаКлиенте
Процедура ТочностьДатыРожденияПриИзменении(Элемент)
	
	УстановитьФорматРедактированияДаты(Элементы.ДатаРождения, Объект.ТочностьДатыРождения);	
	
КонецПроцедуры

&НаКлиенте
Процедура ТочностьДатыСмертиПриИзменении(Элемент)
	
	УстановитьФорматРедактированияДаты(Элементы.ДатаСмерти, Объект.ТочностьДатыСмерти);
	
КонецПроцедуры

&НаКлиенте
Процедура НынеЖивПриИзменении(Элемент)
	
	УстановитьВидимостьГруппыДатыСмерти(Элементы.ГруппаДатыСмерти, Не Объект.НынеЖив);	
	
КонецПроцедуры


